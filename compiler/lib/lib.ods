/*
 * COMS4115: ODDS Standard Library
 *
 * Authors:
 *  - Alex Kalicki
 *  - Alexandra Medway
 *  - Daniel Echikson
 *  - Lilly Wang
 */

/* Return list l with elements in reverse order */
do list_rev = (l) ->
    do aux = (acc, l) ->
        return
            if len(l) == 0 then acc
            else aux(cons(head(l), acc), tail(l))
    return aux([], l)

/* Returns a new list of l's elements with function f applied */
do list_map = (f, l) ->
    do aux = (acc, l) ->
        return
            if len(l) == 0 then list_rev(acc)
            else (() ->
                do applied = f(head(l))
                return aux(cons(applied, acc), tail(l))
            )()
    return aux([], l)

/* Same as list_map, but applied f and discards the return function */
do list_iter = (f, l) ->
    do list_map(f, l)
    return void

/*
 * Return the nth element in the list, wiith the head at position 0. Throw
 * exception with message "list_nth: List index out of range" if list too short
 */
do list_nth = (n, l) ->
    do
        if len(l) <= n then exception("list_nth: List index out of range")
        else void
    return
        if n == 0 then head(l)
        else list_nth(n - 1, tail(l))
